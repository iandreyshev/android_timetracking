apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: 'io.gitlab.arturbosch.detekt'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "ru.iandreyshev.timemanager"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 3
        versionName "1.1.1"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            def signingConfigDirProperty = "signing.config.dir"
            if (project.hasProperty(signingConfigDirProperty)) {
                def signingPropsFileName = project.property("signing.config.properties")
                def propsFile = file(project.property(signingConfigDirProperty) + "/" + signingPropsFileName)

                if (propsFile.exists()) {
                    Properties props = new Properties()
                    props.load(new FileInputStream(propsFile))

                    storeFile file(file(project.property(signingConfigDirProperty) + props['RELEASE_STORE_FILE']))
                    storePassword props['RELEASE_STORE_PASS']
                    keyAlias props['RELEASE_ALIAS']
                    keyPassword props['RELEASE_KEY_PASS']
                }
            } else {
                println "======================================================="
                println "[ERROR] - Please configure release-compilation environment"
                println "======================================================="
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0-RC2'
    implementation "org.jetbrains.anko:anko:0.10.8"

    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta3'
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0-alpha03"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-alpha03"
    implementation 'androidx.core:core-ktx:1.2.0'

    implementation "android.arch.persistence.room:runtime:2.2.0"
    kapt "android.arch.persistence.room:compiler:2.2.0"

    implementation "com.google.android.material:material:1.1.0"

    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxjava:2.2.9"
    implementation "io.reactivex.rxjava2:rxkotlin:2.3.0"

    implementation 'com.androidadvance:topsnackbar:1.1.1'

    implementation 'com.github.captain-miao:optroundcardview:1.1.0'

    implementation 'com.google.firebase:firebase-analytics:17.3.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0-beta04'

    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'androidx.test:runner:1.2.0'

    androidTestImplementation 'junit:junit:4.12'

    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'

    androidTestImplementation 'org.mockito:mockito-core:2.28.2'
}

apply plugin: 'com.google.gms.google-services'
